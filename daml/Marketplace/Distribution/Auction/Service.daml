{-# LANGUAGE MultiWayIf #-}

module Marketplace.Distribution.Auction.Service where

import DA.Action (foldlA)
import DA.Either (partitionEithers)
import DA.Functor (($>))
import DA.Foldable (forA_)
import DA.List (sortOn, mapAccumL)
import DA.Optional (whenSome)
import DA.Finance.Asset (AssetDeposit)
import DA.Finance.Types (Account, Asset, Id)
import DA.Finance.Utils (fetchAndArchive)
import Marketplace.Settlement.Model qualified as Settlement
import Marketplace.Distribution.Auction.Model qualified as Auction
import Marketplace.Distribution.Auction.Model (Auction(..))
import Marketplace.Distribution.Auction.Utils (dutchAuction, splitList, updateBidStatus, withdrawAllocation, generateSettlementInstruction)
import Marketplace.Distribution.Bidding.Model qualified as Bidding
import Marketplace.Rule.AllocationAccount qualified as Allocation
import Marketplace.Rule.AllocationAccount (AllocationAccountRule(..))

type S = Service

template Service
  with
    operator : Party
    provider : Party
    customer : Party
    tradingAccount : Account
    allocationAccount : Account
    receivableAccount : Account
  where
    signatory operator, provider, customer

    controller customer can
      nonconsuming RequestCreateAuction : ContractId CreateAuctionRequest
        with
          auctionId : Text
          asset : Asset
          quotedAssetId : Id
          floorPrice : Decimal
          depositCid : ContractId AssetDeposit
        do
          create CreateAuctionRequest with ..

      nonconsuming CancelAuctionRequest : ()
        with
          createAuctionRequestCid : ContractId CreateAuctionRequest
        do
          archive createAuctionRequestCid

    controller provider can
      nonconsuming CreateAuction : ContractId Auction.T
        with
          createAuctionRequestCid : ContractId CreateAuctionRequest
        do
          CreateAuctionRequest{auctionId; asset; quotedAssetId; floorPrice; depositCid} <- fetchAndArchive createAuctionRequestCid

          -- Deposit the asset into the allocation account so they cannot be transferred/withdrawn without the signatore of the Auctioneer
          depositCid <- exerciseByKey @AllocationAccountRule allocationAccount.id Allocation.Deposit with ..
          create Auction with status = Auction.Open; ..

      nonconsuming RejectAuction : ()
        with
          createAuctionRequestCid : ContractId CreateAuctionRequest
        do
          archive createAuctionRequestCid

      nonconsuming ProcessAuction : (ContractId Auction.T, [ContractId Settlement.SettlementInstruction])
        with
          auctionCid : ContractId Auction.T
          bidCids : [ContractId Bidding.Bid]
        do
          auction@Auction{floorPrice ,asset , depositCid, status} <- fetch auctionCid
          assertMsg "Auction already successfully processed" (status `elem` [Auction.Open, Auction.NoValidBids])

          bids <- forA bidCids fetch
          -- Split bids on those above / below the floor price -> Sort valid bids descendingly -> Execute Auction -> Split auction result on allocated / unallocated (ie, zero allocation)
          let
            (invalidBids, validBids) = partitionEithers $ splitList (\bid -> bid.details.price >= floorPrice) bids
            sortedBids = sortOn (\bid -> Down bid.details) validBids
            ((remaining, finalPrice), allocations) = mapAccumL dutchAuction (asset.quantity, 0.0) sortedBids
            (allocatedBids, unallocatedBids) = partitionEithers $ splitList (\allocation -> allocation.quantity == 0.0) allocations

          -- Return assets of all invalid and unallocated bids. Also update their bid status accordingly
          forA_ invalidBids $ withdrawAllocation *> updateBidStatus Bidding.Invalid
          forA_ ((.bid) `map` unallocatedBids) $ withdrawAllocation *> updateBidStatus Bidding.NoAllocation

          -- Generate Settlement instructions for allocated bids
          let settleAllocations = (\allocation -> Auction.SettleAllocation with allocation; price = finalPrice; issuer = customer; issuerReceivableAccount = receivableAccount) <$> allocatedBids
          (remainingDepositCidOpt, siCids) <- foldlA generateSettlementInstruction (Some depositCid, []) settleAllocations

          -- Transfer remaining deposit back to the Issuer's trading account
          whenSome remainingDepositCidOpt \depositCid -> (exerciseByKey @AllocationAccountRule allocationAccount.id Allocation.Withdraw with depositCid = depositCid; transferTo = tradingAccount) $> ()

          -- Update Auction contract with the outcome of the auction processing
          let newStatus = if
                | remaining == asset.quantity -> Auction.NoValidBids
                | remaining > 0.0             -> Auction.PartiallyAllocated with ..
                | otherwise                   -> Auction.FullyAllocated with ..
          auctionCid <- archive auctionCid *> create auction with status = newStatus

          pure (auctionCid, siCids)

template Offer
  with
    operator : Party
    provider : Party
    customer : Party
  where
    signatory operator, provider

    controller customer can
      Accept : ContractId Service
        with
          tradingAccount : Account
          allocationAccount : Account
          receivableAccount : Account
        do
          create Service with ..

      Decline : ()
        do
          return ()

    controller provider can
      Withdraw : ()
        do pure ()

template Request
  with
    provider : Party
    customer : Party
    tradingAccount : Account
    allocationAccount : Account
    receivableAccount : Account
  where
    signatory customer

    controller customer can
      Cancel : ()
        do pure ()

    controller provider can
      Reject : ()
        do pure ()

    choice Approve : ContractId Service
      with
        operator : Party
      controller operator, provider
      do
        create Service with ..

template CreateAuctionRequest
  with
    operator : Party
    provider : Party
    customer : Party
    auctionId : Text
    asset : Asset
    quotedAssetId : Id
    floorPrice : Decimal
    depositCid : ContractId AssetDeposit
  where
    signatory operator, provider, customer
