daml 1.2
module Marketplace.Custodian where

import Marketplace.Registry
import Marketplace.Token
import Marketplace.Utils

import DA.Finance.Asset
import DA.Finance.Types

import DA.Action
import DA.Next.Set


template CustodianRelationship
  with
    operator : Party
    custodian : Party
    party : Party
  where
    signatory custodian, party
    key (custodian, operator, party) : (Party, Party, Party)
    maintainer key._1


template CustodianRelationshipRequest
  with
    operator : Party
    requester : Party
    custodian : Party
    role : MarketRole
  where
    signatory operator, requester

    controller custodian can
      CustodianRelationshipRequest_Approve : ContractId CustodianRelationship
        do
          case role of
            IssuerRole -> void $ exerciseByKey @Custodian (operator, custodian) Custodian_AddIssuer with issuer = requester
            ExchangeRole -> void $ exerciseByKey @Custodian (operator, custodian) Custodian_AddExchange with exchange = requester
            InvestorRole -> void $ exerciseByKey @Custodian (operator, custodian) Custodian_AddInvestor with investor = requester
            BrokerRole -> void $ exerciseByKey @Custodian (operator, custodian) Custodian_AddBroker with broker = requester
            _ -> return ()
          create CustodianRelationship with party = requester, ..

      CustodianRelationshipRequest_Reject : ()
        do return ()


template CustodianInvitation
  with
    operator : Party
    custodian : Party
    public : Party
  where
    signatory operator

    key (operator, custodian) : (Party, Party)
    maintainer key._1

    controller custodian can
      CustodianInvitation_Accept : ContractId Custodian
        with
          name : Text
          location : Text
        do
          create RegisteredCustodian with ..
          create Custodian with investors = [], exchanges = [], brokers = [], issuers = [], ..


template Custodian
  with
    operator : Party
    custodian : Party
    issuers  : [Party]
    investors : [Party]
    brokers : [Party]
    exchanges : [Party]
  where
    signatory operator, custodian

    key (operator, custodian) :  (Party, Party)
    maintainer key._1

    controller custodian can
      Custodian_AddIssuer : ContractId Custodian
        with
          issuer : Party
        do
          assertMsg ("Issuer " <> show issuer <> " already exists") $ issuer `notElem` issuers
          create this with issuers = issuer :: issuers

      Custodian_AddExchange : ContractId Custodian
        with
          exchange : Party
        do
          assertMsg ("Exchange " <> show exchange <> " already exists") $ exchange `notElem` exchanges
          create this with exchanges = exchange :: exchanges

      Custodian_AddInvestor : ContractId Custodian
        with
          investor : Party
        do
          assertMsg ("Investor " <> show investor <> " already exists") $ investor `notElem` investors
          create this with investors = investor :: investors

      Custodian_AddBroker : ContractId Custodian
        with
          broker : Party
        do
          assertMsg ("Broker " <> show broker <> " already exists") $ broker `notElem` brokers
          create this with brokers = broker :: brokers

      nonconsuming Custodian_GetAccount : Account
        with
          investor : Party
        do
          let accountId = getAccountId investor custodian [custodian]
          return Account with id = accountId, provider = custodian, owner = investor

      nonconsuming Custodian_CreateDeposit : ContractId AssetDeposit
        with
          tokenId : Id
          depositQuantity : Decimal
          beneficiary : Party
        do
          (tokenCid, token) <- fetchByKey @Token tokenId
          let quantity = roundBankers token.quantityPrecision depositQuantity
              asset = Asset with id = tokenId, ..
              depositObs = fromList $ beneficiary :: signatory token
          account <- exercise self Custodian_GetAccount with investor = beneficiary
          create AssetDeposit with observers = depositObs, account = account, ..
