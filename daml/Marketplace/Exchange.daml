module Marketplace.Exchange where

import qualified Marketplace.BinaryOption as BinaryOption
import Marketplace.Custodian
import Marketplace.ExchangeParticipant
import Marketplace.Registry
import qualified Marketplace.Notification as Notification
import Marketplace.Token
import Marketplace.Trading
import Marketplace.Utils

import DA.Finance.Types

import DA.List
import DA.Next.Set hiding (delete)


template ExchangeInvitation
  with
    operator : Party
    exchange : Party
    public : Party
  where
    signatory operator

    key (operator, exchange) : (Party, Party)
    maintainer key._1

    controller exchange can
      ExchangeInvitation_Accept : ContractId Exchange
        with
          name : Text
          location : Text
        do
          create RegisteredExchange with ..
          create Exchange with tokenPairs = [], binOptions = [], participants = [], ..


-- |Request from an investor or broker to be added to the exchange
template ExchangeParticipantInvitationRequest
  with
    operator : Party
    exchange : Party
    participant : Party
  where
    signatory participant
    key (operator, exchange, participant) : (Party, Party, Party)
    maintainer key._3

    controller exchange can
      ExchangeParticipantInvitationRequest_Accept : ContractId ExchangeParticipant
        do
          create Notification.Dismissible with
            sender = exchange, senderRole = Some ExchangeRole
            receiver = participant, text = "Exchange participant request accepted."
          exerciseByKey @Exchange (operator,exchange) Exchange_AddParticipant with exchParticipant = participant
          create ExchangeParticipant with exchParticipant = participant, ..

      ExchangeInvtationRequest_Reject : ()
        do
          create Notification.Dismissible with
            sender = exchange, senderRole = Some ExchangeRole
            receiver = participant, text = "Exchange participant request accepted."
          return ()


template Exchange
  with
    operator : Party
    exchange : Party
    public : Party
    tokenPairs : [IdPair] -- tokens pairs that are tradable on the exchange
    binOptions : [IdPair]
    participants : [Party]
  where
    signatory operator, exchange

    key (operator, exchange) : (Party, Party)
    maintainer key._1

    observer participants

    controller exchange can
      nonconsuming Exchange_RequestCustodianRelationship : ContractId CustodianRelationshipRequest
        with
          custodian : Party
        do create CustodianRelationshipRequest with requester = exchange, role = ExchangeRole, ..

      Exchange_InviteParticipant : (ContractId Exchange, ContractId ExchangeParticipantInvitation)
        with
          exchParticipant : Party
        do
          participantInvitationCid <- create ExchangeParticipantInvitation with ..
          exchangeCid <- create this with participants = dedup $ exchParticipant :: participants
          return (exchangeCid, participantInvitationCid)

      Exchange_AddParticipant : ContractId Exchange
        with
          exchParticipant : Party
        do
          create this with participants = dedup $ exchParticipant :: participants

      Exchange_RemovePair : ContractId Exchange
        with
          pair : IdPair
        do
          create this with tokenPairs = delete pair tokenPairs

      Exchange_AddPair : ContractId Exchange
        with
          baseTokenId : Id
          quoteTokenId : Id
          minQuantity : Decimal
          maxQuantity : Decimal
        do
          (_, baseToken) <- fetchByKey @Token baseTokenId
          (_, quoteToken) <- fetchByKey @Token quoteTokenId

          assertMsg ("minimum quantity should be rounded to at most " <> show baseToken.quantityPrecision <> " decimal places")
            $ roundBankers baseToken.quantityPrecision minQuantity == minQuantity
          assertMsg ("max quantity should be rounded to at most " <> show baseToken.quantityPrecision <> " decimal places")
            $ roundBankers baseToken.quantityPrecision maxQuantity == maxQuantity

          assertMsg "minimum quantity should be less than the maximum quantity"
            $ minQuantity < maxQuantity

          assertMsg "Pair must have different base and quote tokens" $ baseToken.id.label /= quoteToken.id.label
          let instrumentName = baseToken.id.label <> quoteToken.id.label
          let newMarket = MarketPair with
                id = Id with signatories = fromList [exchange], label = instrumentName, version = 0
                exchange
                description = baseToken.id.label <> " vs " <> quoteToken.id.label
                baseTokenId
                quoteTokenId
                calendarId = "1261007448"
                pricePrecision = quoteToken.quantityPrecision
                quantityPrecision = baseToken.quantityPrecision
                minQuantity = minQuantity : Decimal
                maxQuantity = maxQuantity : Decimal
                status = InstrumentActive
                observers = fromList participants
          create newMarket
          create this with tokenPairs = dedup $ (baseTokenId, quoteTokenId) :: tokenPairs

      Exchange_AddBinaryOption : ContractId Exchange
        with
          binOptionId : Id
          quoteTokenId : Id
        do
          (_, binOption) <- fetchByKey @BinaryOption.T binOptionId
          (_, quoteToken) <- fetchByKey @Token quoteTokenId
          create this with binOptions = dedup $ (binOptionId, quoteTokenId) :: binOptions

      nonconsuming Exchange_ApproveOrderRequest : ContractId Order
        with
          orderRequestCid : ContractId OrderRequest
          orderId : Int
        do
          orderRequest <- fetch orderRequestCid
          let pair = orderRequest.order.pair
          assertMsg ("pair " <> pair._1.label <> "/" <> pair._2.label <> " is not supported by exchange")
            $ (pair `elem` tokenPairs) || (pair `elem` binOptions)
          exercise orderRequestCid OrderRequest_Ack with ..
